   LIST P=18F4550
#include <p18f4550.inc>    
            
;The high-priority interrupt vector is at 000008h and the low-priority interrupt vector is at 000018h.
   CONFIG FOSC = INTOSCIO_EC
   CONFIG WDT = OFF
   CONFIG MCLRE =OFF
   CONFIG PBADEN = ON
   CONFIG LVP=OFF
   ORG 0X0000
   GOTO MAIN
   
   ORG 0X0008
   GOTO SERVICIOHP   
   
   ORG 0X0018
   RETFIE
   
   CBLOCK 0X20  ;### VARIABLES###
   NUMERO
   REG1
   REG2
   WTEMP
   UNIDAD ;0X24
   DECENA ;0X25
   CENTENA; 0X26
   BANDERAS
   UNIDAD1
   DECENA1
   CENTENA1
   BANDERA2 ;0X2B
   CERO
   UNIDAD2 ;0X2D
   DECENA2; 0X2E
   CENTENA2 ;2F
   NUMEROH 
   NUMEROL
   NUMERO1H
   NUMERO1L 
   NUMERO2H 
   NUMERO2L 
   CUENTA ;0X36
   ENDC
   #DEFINE L1 PORTB,0
   #DEFINE L2 PORTB,1
   #DEFINE L3 PORTB,2
   #DEFINE L4 PORTB,3
   #DEFINE C1 PORTB,4
   #DEFINE C2 PORTB,5
   #DEFINE C3 PORTB,6
   #DEFINE C4 PORTB,7
   #DEFINE RS PORTE,0
   #DEFINE RW PORTE,1
   #DEFINE E PORTE,2
   #DEFINE ENTER BANDERA2,0
   #DEFINE CAMBIO BANDERA2,1
   #DEFINE SM BANDERAS,0
   #DEFINE RST BANDERAS,1
   #DEFINE ML BANDERAS,2
   #DEFINE DV BANDERAS,3
   #DEFINE CP BANDERAS,4
   #DEFINE POT2 BANDERAS,5
   #DEFINE POT BANDERAS,6
   #DEFINE FACT BANDERAS,7
 
   
;Iniciamos:
MAIN
   CALL INIT_OSC
   CALL INIT_PORTS
   CALL INIT_INTERRUPCIONES
INICIO
   CALL INIT_VARIABLES 
   CALL RET_200MS
   CALL RET_200MS
   CALL CONFIG_8B ;AQUI SE CONFIGURA EN MODO 8 BITS
   BSF INTCON,7
WORM_HOLE NOP
   GOTO WORM_HOLE
   
INIT_OSC
   CLRF OSCTUNE,0
   MOVLW B'01110110'
   MOVWF OSCCON
   RETURN
INIT_PORTS
   MOVLW B'00001111'
   MOVWF LATB
   MOVWF ADCON1
   MOVLW B'11110000'
   MOVWF TRISB
   MOVLW H'0F'
   MOVWF TRISA;CONFIGURAMOS A PARA PRUEBAS
   CLRF TRISD
   CLRF TRISE
   CLRF LATD
   CLRF LATE
   CLRF LATA
   CLRF TRISC
   RETURN
INIT_INTERRUPCIONES
   BSF	INTCON2,7 ; DESHABILITA LAS PULL UPS EN EL PUERTO B
   CLRF INTCON
   BSF INTCON,7; HABILITAMOS EL GLOBAR INTERRUPT
   BSF INTCON,3 ;HABILITAMOS LAS INTERRUPCIONES POR EL PORTB
   BSF INTCON2,0 ; LAS INTERRUPCIONES DEL PUERTO B SERAN DE ALTA PRIORIDAD
   RETURN
INIT_VARIABLES
   CLRF NUMERO
   CLRF CERO
   CLRF NUMEROH
   CLRF NUMEROL
   CLRF NUMERO1L
   CLRF NUMERO1H
   CLRF NUMERO2L
   CLRF NUMERO2H
   CLRF CUENTA
   CLRF UNIDAD
   CLRF DECENA
   CLRF CENTENA
   CLRF UNIDAD1
   CLRF DECENA1
   CLRF CENTENA1
   CLRF UNIDAD2
   CLRF DECENA2
   CLRF CENTENA2
   CLRF BANDERAS
   CLRF REG1
   CLRF REG2
   CLRF WTEMP
   CLRF CERO
   RETURN
INITS_LCD
   BCF RS
   MOVWF LATD
   BSF E
   CALL RET_20MS
   BCF E
   RETURN
CONFIG_8B
   CALL RET_20MS
   MOVLW H'38'
   CALL INITS_LCD
   MOVLW H'06'
   CALL INITS_LCD
   MOVLW H'0E';REALMENTE ES 0C PERO ESTOY PROBANDO PARA VER EL CURSOR
   CALL INITS_LCD
   MOVLW H'01'
   CALL INITS_LCD
   RETURN
   
   
;################ INTERRUPCIONES HP ###############################   
   
SERVICIOHP 
   MOVWF WTEMP
   BCF INTCON,7  ; DESHABILITAMOS EL GLOBAR INTERRUPT
   BTFSC C1
   CALL COLUMNA1 ; COMPROBAMOS QUE EL BOTON PRESIONADO ESTA EN LA COLUMNA 1
   BTFSC C2
   CALL COLUMNA2 ; COMPROBAMOS QUE EL BOTON PRESIONADO ESTA EN LA COLUMNA 2
   BTFSC C3
   CALL COLUMNA3 ; COMPROBAMOS QUE EL BOTON PRESIONADO ESTA EN LA COLUMNA 3
   BTFSC C4
   CALL COLUMNA4 ; COMPROBAMOS QUE EL BOTON PRESIONADO ESTA EN LA COLUMNA 4
   
AR 		 ;ANTI REBOTE DEL TECLADO
   MOVLW .15
   CPFSEQ PORTB
   GOTO AR
   BCF INTCON,0  ; LIMPIA LA BANDERA DE INTERRUPCION POR CAMBIO EN RB
   BCF INTCON,1  ; LIMPIA LA BANDERA DE INTERRUPCION EXTERNA
   BSF INTCON,7  ; HABILITAMOS EL GLOBAR INTERRUPT
   MOVF	WTEMP,W
   RETFIE
   
COLUMNA1  
   BCF L1
   BTFSS C1
   MOVLW .49
   BSF L1
   BCF L2
   BTFSS C1
   MOVLW .52
   BSF L2
   BCF L3
   BTFSS C1
   MOVLW .55
   BSF L3
   BCF L4
   BTFSS C1
   GOTO CLEAR; CLEAR
   BSF L4
   BCF C1
   MOVWF NUMERO ;;SE GUARDA EN NUMERO, PARA MOSTRAR PRIMERO Y LUEGO GUARDAR EL NUMERO
   CALL DATO
   CALL GUARDAR
   INCF CUENTA,1
SALIDA
   RETURN
   
COLUMNA2
   BCF L1
   BTFSS C2
   MOVLW .50
   BSF L1
   BCF L2
   BTFSS C2
   MOVLW .53
   BSF L2
   BCF L3
   BTFSS C2
   MOVLW .56
   BSF L3
   BCF L4
   BTFSS C2
   MOVLW .48
   BSF L4
   BCF C2
   MOVWF NUMERO
   CALL DATO
   CALL GUARDAR
   INCF CUENTA,1
   RETURN
   
COLUMNA3
   BCF L1
   BTFSS C3
   MOVLW .51
   BSF L1
   BCF L2
   BTFSS C3
   MOVLW .54
   BSF L2
   BCF L3
   BTFSS C3
   MOVLW .57
   BSF L3
   BCF L4
   BTFSS C3
   GOTO FUNCION_ENTER ;ENTER
   MOVWF NUMERO
   CALL DATO
   CALL GUARDAR
   INCF CUENTA,1
SALIDA2
   BSF L4
   BCF C3
   RETURN
 
COLUMNA4
   BTFSC CAMBIO ;PRUEBA SI CAMBIO ESTA ACTIVO, SI NO ESTA ACTIVO, USA LAS PRIMERAS OPERACIONES
   GOTO OTRO_OPE
   BCF L1
   BTFSS C4
   MOVLW .8
   BSF L1
   BCF L2
   BTFSS C4
   MOVLW .4
   BSF L2
   BCF L3
   BTFSS C4
   MOVLW .2
   BSF L3
   BCF L4
   BTFSS C4
   MOVLW .1
   BSF L4
   GOTO FIN
OTRO_OPE ; EN CASO DE QUE CAMBIO ESTE ACTIVADO, SE VA A ESTA TABLA PARA COMPARACION, POTENCIA Y FACTORIAL
   BCF L1
   BTFSS C4
   MOVLW .128
   BSF L1
   BCF L2
   BTFSS C4
   MOVLW .64
   BSF L2
   BCF L3
   BTFSS C4
   MOVLW .32
   BSF L3
   BCF L4
   BTFSS C4
   MOVLW .16
   BSF L4
FIN
   MOVWF BANDERAS ;ACTIVAMOS LAS BANDERAS
   CALL TAB_OPE	   ; DE AQUI SACAMOS EL CARACTER A MOSTRAR	
   CALL DATO  ; MOSTRAMOS
   MOVF UNIDAD,0  ;YA QUE YA ELEGIMOS LA OPERACION, LO GUARDAMOS EN UNIDAD 1 Y ASI
   MOVWF UNIDAD1
   MOVF DECENA,0
   MOVWF DECENA1
   MOVF CENTENA,0
   MOVWF CENTENA1
   CLRF UNIDAD
   CLRF DECENA
   CLRF CENTENA
   MOVLW .5
   MOVWF CUENTA
   RETURN

OPERAR
   MOVF CENTENA1,W ;INICIO DE PREPARACION PARA LAS OPERACIONES
   MOVWF CENTENA
   MOVF DECENA1,W
   MOVWF DECENA
   MOVF UNIDAD1,W
   MOVWF UNIDAD
   CALL CONVERSION_BCD_BIN
   MOVF NUMEROH,W
   MOVWF NUMERO1H
   MOVF NUMEROL,W
   MOVWF NUMERO1L ; FIN DE NUMERO 1
   MOVF CENTENA2,W
   MOVWF CENTENA
   MOVF DECENA2,W
   MOVWF DECENA
   MOVF UNIDAD2,W
   MOVWF UNIDAD
   CALL CONVERSION_BCD_BIN
   MOVF NUMEROH,W
   MOVWF NUMERO2H
   MOVF NUMEROL,W
   MOVWF NUMERO2L 
   CLRF NUMEROH 
   CLRF NUMEROL ; FIN DE LA PREPARACION PARA LAS OPERACIONES
   
   BTFSC SM
   CALL SUMAR
   BTFSC RST
   CALL RESTAR
   BTFSC ML
   CALL MULTIPLICAR
   BTFSC DV
   CALL DIVIDIR
   BTFSC CP
   GOTO COMPARAR
   BTFSC POT2
   CALL CUADRADO
   BTFSC POT 
   CALL POTENCIA
   BTFSC FACT 
   CALL FACTORIAL
   GOTO CHECK
SALIR_CHECK
   CALL CONVERSION_BIN_BCD
   CALL MOSTRAR_RESULTADO ;<<<< AQUI SE VA A MOSTRAR UNIDAD, DECENA Y CENTENA
   BSF INTCON,7
   RETURN
     
SUMAR 
   MOVF NUMERO1L,W ;W= NUMERO1L
   ADDWF NUMERO2L,W ;W= NUMERO1L + NUMERO2L
   MOVWF NUMEROL ;NUMEROL= RESULTADO 
   BTFSC STATUS,C 
   INCF NUMERO1H,F ; INCREMENTO EN NUMERO1H DE ACTIVARSE EL CARRY
   MOVF NUMERO1H,W ;W= NUMERO1H
   ADDWF NUMERO2H,W ;W= NUMERO1H + NUMERO2H
   MOVWF NUMEROH ;NUMEROH= W
   RETURN

RESTAR
   MOVF NUMERO2L,0 ;W= NUMERO1L
   SUBWF NUMERO1L,0 ;W= NUMERO1L - NUMERO2L
   MOVWF NUMEROL ;NUMEROL= RESULTADO 
   BTFSS STATUS,C 
   DECF NUMERO1H,1 ; DECREMENTO EN NUMERO1H DE NO ACTIVARSE EL CARRY
   MOVF NUMERO2H,0 ;W= NUMERO2H
   SUBWF NUMERO1H,0 ;W= NUMERO1H - NUMERO2H
   MOVWF NUMEROH ;NUMEROH= W 
   RETURN
   
MULTIPLICAR ; UNIDAD DECENA CENTENA AGARRADAS DEL SEGUNDO NUMERO EN BCD
   CALL IOPE
   CLRF NUMEROH
   CLRF NUMEROL
REP_UNI   ;INICIO DE LA MULTIPLICACION
   DECF UNIDAD
   BTFSS STATUS,C
   GOTO REP_DEC
   MOVF NUMERO1L,0      ;W= NUMERO1L
   ADDWF NUMEROL,1      ;NUMEROL= NUMEROL + NUMERO1L
   BTFSC STATUS,C 	
   GOTO OPEN
   MOVF NUMERO1H,0       ;W= NUMERO1H
CLOSE ADDWF NUMEROH,1     ;NUMEROH= NUMEROH + NUMERO1H
   GOTO REP_UNI
OPEN INCF NUMERO1H,0       ;W= NUMERO1H+1
   GOTO CLOSE
REP_DEC 
   DECF DECENA,1
   BTFSS STATUS,C
   GOTO REP_CEN
   MOVLW .10
   MOVWF UNIDAD		;UNIDAD= 10
   GOTO REP_UNI   
REP_CEN
   DECF CENTENA,1
   BTFSS STATUS,C
   GOTO RMULT
   MOVLW .10
   MOVWF DECENA		;DECENA= 10
   GOTO REP_DEC  
RMULT 
   CLRF UNIDAD
   CLRF DECENA
   CLRF CENTENA
   RETURN

IOPE
   MOVF UNIDAD2,W
   MOVWF UNIDAD
   MOVF DECENA2,W
   MOVWF DECENA
   MOVF CENTENA2,W
   MOVWF CENTENA
   RETURN
   
DIVIDIR
   CALL CONDICION
LOOP_DIVIDIR1 MOVF NUMERO2L,0 ;W= NUMERO2L
   SUBWF NUMERO1L,1 ;NUMERO1L= NUMERO1L - NUMERO2L
   BTFSS STATUS,C 
   GOTO BAJAR_H
LOOP_DIVIDIR2 MOVF NUMERO2H,0 ;W= NUMERO2H
   SUBWF NUMERO1H,1 ;NUMERO1H= NUMERO1H - NUMERO2H
   BTFSS STATUS,C
   GOTO RESULTADO_DIVI
   INCF NUMEROL,1   
   BTFSC STATUS,C
   INCF NUMEROH,1
   GOTO LOOP_DIVIDIR1
BAJAR_H 
   DECF NUMERO1H,1 ; DECREMENTO EN NUMERO1H DE NO ACTIVARSE EL CARRY   
   BTFSC STATUS,C
   GOTO LOOP_DIVIDIR2
RESULTADO_DIVI 
   RETURN

CONDICION    
   MOVLW .0
   CPFSGT NUMERO2L
   CPFSEQ NUMERO2H
   RETURN
   MOVLW H'5C'
   CALL DATO
   RETURN
   
COMPARAR
   MOVF NUMERO2H,0
   CPFSEQ NUMERO1H 
   GOTO COMP1
   MOVF NUMERO2L,0
   CPFSEQ NUMERO1L
   GOTO COMP2
   GOTO IGUAL   
COMP1 CPFSGT NUMERO1H ; NUMERO1H > NUMERO2H?
   GOTO MENOR
   GOTO MAYOR   
COMP2 CPFSGT NUMERO1L ; NUMERO1L > NUMERO2L?
   GOTO MENOR
   GOTO MAYOR      
IGUAL
   MOVF UNIDAD1,W
   MOVWF UNIDAD
   MOVF DECENA1,W
   MOVWF DECENA
   MOVF CENTENA1,W
   MOVWF CENTENA
   CALL MOSTRAR_RESULTADO
   MOVLW H'3D'
   CALL DATO
   MOVF UNIDAD2,W
   MOVWF UNIDAD
   MOVF DECENA2,W
   MOVWF DECENA
   MOVF CENTENA2,W
   MOVWF CENTENA
   CALL MOSTRAR_RESULTADO
   RETURN
MENOR
   MOVF UNIDAD1,W
   MOVWF UNIDAD
   MOVF DECENA1,W
   MOVWF DECENA
   MOVF CENTENA1,W
   MOVWF CENTENA
   CALL MOSTRAR_RESULTADO
   MOVLW H'3C'
   CALL DATO
   MOVF UNIDAD2,W
   MOVWF UNIDAD
   MOVF DECENA2,W
   MOVWF DECENA
   MOVF CENTENA2,W
   MOVWF CENTENA
   CALL MOSTRAR_RESULTADO
   RETURN       
MAYOR
   MOVF UNIDAD1,W
   MOVWF UNIDAD
   MOVF DECENA1,W
   MOVWF DECENA
   MOVF CENTENA1,W
   MOVWF CENTENA
   CALL MOSTRAR_RESULTADO
   MOVLW H'3E'
   CALL DATO
   MOVF UNIDAD2,W
   MOVWF UNIDAD
   MOVF DECENA2,W
   MOVWF DECENA
   MOVF CENTENA2,W
   MOVWF CENTENA
   CALL MOSTRAR_RESULTADO
   RETURN   
   
CUADRADO
   CLRF NUMEROH
   CLRF NUMEROL
   MOVF UNIDAD1,W
   MOVWF UNIDAD
   MOVF DECENA1,W
   MOVWF DECENA
   MOVF CENTENA,W
   MOVWF CENTENA
   CALL REP_UNI
   RETURN
   
POTENCIA
   CLRF NUMEROH
   CLRF NUMEROL
   TSTFSZ NUMERO2H
   GOTO CPOT
   TSTFSZ NUMERO2L
   GOTO CPOT1
   CLRF NUMEROH ; DE SER 0, SE FUERZA MOSTRAR UN 1 EN EL RESULTADO
   MOVLW .1
   MOVWF NUMEROL
   RETURN 
CPOT1    MOVLW .1
   CPFSEQ NUMERO2L
   GOTO CPOT
   MOVF NUMERO1L,W
   CALL FACT_POT
   RETURN
  
CPOT   
   DCFSNZ NUMERO2L,F
   GOTO AJUST
   MOVF UNIDAD1,W ; MODULO PARA MULTIPLICAR CUALQUIER PRIMER NUMERO POR SI MISMO
   MOVWF UNIDAD
   MOVF DECENA1,W
   MOVWF DECENA
   MOVF CENTENA,W
   MOVWF CENTENA
   CALL REP_UNI   ;FIN DEL MODULO
   CALL FACT_POT0 ; MODULO DE ACUMULACION
   GOTO CPOT

AJUST
   DECF NUMERO2H,F
   BTFSC STATUS,C
   GOTO CPOT
   CALL FACT_POT
   RETURN
   
FACTORIAL
   TSTFSZ NUMERO1H
   GOTO IFACT
   TSTFSZ NUMERO1L
   GOTO IFACT
   CLRF NUMEROH   ; DE SER 0, SE FUERZA MOSTRAR UN 1 EN EL RESULTADO
   MOVLW .1
   MOVWF NUMEROL
   RETURN 
IFACT   
   CLRF NUMEROL
   CLRF NUMEROH
CFACT0
   DCFSNZ UNIDAD1,W
   GOTO CFACT1
   MOVWF UNIDAD1
   MOVWF UNIDAD
   CALL REP_UNI  ; MODULO DE MULTIPLICACION
   CALL FACT_POT0 ; MODULO DE ACUMULACION
   GOTO CFACT0
CFACT1
   DECF DECENA1,F
   BTFSS STATUS,C
   GOTO CFACT2
   MOVLW .10
   MOVWF UNIDAD1
   GOTO CFACT0 
CFACT2
   DECF CENTENA1,F
   BTFSS STATUS,C
   GOTO RFACT
   MOVLW .10
   MOVWF UNIDAD1
   GOTO CFACT1   
   
RFACT 
   CALL FACT_POT
   RETURN

FACT_POT0   
   MOVF NUMEROH,W
   MOVWF NUMERO1H
   MOVF NUMEROL,W
   MOVWF NUMERO1L
   CLRF NUMEROH
   CLRF NUMEROL
   RETURN
   
FACT_POT
   MOVF NUMERO1H,W
   MOVWF NUMEROH
   MOVF NUMERO1L,W
   MOVWF NUMEROL
   RETURN
   
CHECK   
       MOVLW .3
       CPFSGT NUMEROH ;COMPROBACION DE QUE SE CUMPLA: NUMEROH>3
       GOTO CHECK1
       GOTO O_F ;SEÑAL DE OVERFLOW EN LA OPERACION
CHECK1 CPFSEQ NUMEROH ;COMPROBACION DE QUE SE CUMPLA: NUMEROH=3
       GOTO SALIR_CHECK
       MOVLW .231
       CPFSGT NUMEROL ;COMPROBACION DE QUE SE CUMPLA: NUMEROL>231
       GOTO SALIR_CHECK
       GOTO O_F ;SEÑAL DE OVERFLOW EN LA OPERACION

O_F 
    MOVLW H'4F' ; O
    CALL DATO
    MOVLW H'56' ; V
    CALL DATO
    MOVLW H'45' ; E
    CALL DATO
    MOVLW H'52' ; R
    CALL DATO
    MOVLW H'20' ; 
    CALL DATO
    MOVLW H'46' ; F
    CALL DATO
    MOVLW H'4C' ; L
    CALL DATO
    MOVLW H'4F' ; O
    CALL DATO
    MOVLW H'57' ; W
    CALL DATO
    RETURN
    
DATO 
   BSF RS
   MOVWF LATD
   BSF E
   CALL RET_20MS
   BCF E
   RETURN   
    
MOSTRAR_RESULTADO
   MOVF CENTENA,0
   CPFSLT CERO  ; TESTEA SI CENTENA ES 0
   GOTO MDEC 	; EN CASO DE QUE SI SEA 0, NO MUESTRA NADA Y SE VA A DECENA
   ADDLW H'30'  ; EN CASO DE QUE NO SEA 0, SE LE SUMA H.30 PARA LLEVAR EL NUMERO A ASCII Y ASI MOSTRATLO
   CALL DATO    ; LEVA EL NUMERO A LA PANTALLA
MDEC
   MOVLW .0 
   CPFSGT CENTENA ;COMPARAMOS CON CENTENA, SI CENTENA ES 0
   CPFSEQ DECENA  ; SI CENTENA ES 0, PREGUNTAMOS SI DECENA TAMBIEN LO ES. PARA NO MOSTRAR CEROS
   GOTO MOSTRAR_DEC ;EN CASO DE QUE CENTENA NO SEA 0, MUESTRA LO QUE HAYA EN DECENA, ASI SEA 0
   GOTO MUNI	;SI DECENA ES 0, NO LO MUESTRA
MOSTRAR_DEC
   MOVF DECENA,0
   ADDLW H'30'
   CALL DATO
MUNI
   MOVF UNIDAD,0 ; MUESTRA LO QUE HAYA EN UNIDAD, ASI SEA 0
   ADDLW H'30' 
   CALL DATO
   RETURN

CONVERSION_BIN_BCD  
  CALL QUITAR_CENTENA
  CALL QUITAR_DECENA
  CALL QUITAR_UNIDAD
  RETURN

QUITAR_CENTENA
    CLRF CENTENA             ; REINICIA EL VALOR DE CENTENAS A 0
    MOVLW B'01100100'        ; CARGA W=100
LOOP_C2:
    SUBWF NUMEROL,F          ; RESTAMOS 100 A NUMEROL
    BTFSS STATUS,C           ; SI HAY CARRY, LA RESTA FUE EXITOSA
    GOTO BAJAR_H1
STEP    INCF CENTENA,F       ; INCREMENTA LAS CENTENAS EN 1
    GOTO LOOP_C2             ; REPITE EL BUCLE
BAJAR_H1 
    DECF NUMEROH,F
    BTFSC STATUS,C	     ; SI NO SE ACTIVA EL CARRY, NUMEROH=0
    GOTO STEP	             ; REPITE EL BUCLE, NO SIN ANTES AUMENTAR LAS CENTENAS
    ADDWF NUMEROL,F	     ; SE DEVUELVE LA ULTIMA RESTA EN NUMEROL
    INCF NUMEROH,F	     ; SE DEVUELVE EL ULTIMO DECREMENTO EN NUMEROH
    RETURN                   ; ESCAPE DE LA SUBRUTINA, SOLO SE ACTIVA SI NUMEROL<100 Y NUMEROH=0
        
QUITAR_DECENA CLRF DECENA    ; REINICIA EL VALOR DE DECENA A 0
    MOVLW B'00001010'        ; CARGA W=10
LOOP_D2 SUBWF NUMEROL,F      ; RESTAMOS 10 A NUMEROL
    BTFSS STATUS,C           ; SI HAY CARRY, LA RESTA FUE EXITOSA
    GOTO SALIR_D 
    INCF DECENA,F            ; INCREMENTA LAS DECENAS EN 1
    GOTO LOOP_D2             ; REPITE EL BUCLE
SALIR_D ADDWF NUMEROL,F	     ; SE DEVUELVE LA ULTIMA RESTA EN NUMEROL
    RETURN                   ; ESCAPE DE LA SUBRUTINA, SOLO SE ACTIVA SI NUMEROL<10 Y NUMEROH=0
QUITAR_UNIDAD CLRF UNIDAD    ; REINICIA EL VALOR DE UNIDAD A 0
LOOP_U2 DECF NUMEROL,F       ; RESTAMOS 1 A NUMEROL
    BTFSS STATUS,C           ; SI HAY CARRY, LA RESTA FUE EXITOSA
    GOTO SALIR_U
    INCF UNIDAD,F            ; INCREMENTA LAS DECENAS EN 1
    GOTO LOOP_U2             ; REPITE EL BUCLE
SALIR_U INCF NUMEROL,F       ; SE DEVUELVE LA ULTIMA RESTA EN NUMEROL
    RETURN                   ; ESCAPE DE LA SUBRUTINA, SOLO SE ACTIVA SI NUMEROL<10 Y NUMEROH=0   
   
CONVERSION_BCD_BIN
   CLRF NUMEROL
   CLRF NUMEROH
   TSTFSZ CENTENA
   CALL AGREGAR_CENTENA
   TSTFSZ DECENA
   CALL AGREGAR_DECENA
   TSTFSZ UNIDAD
   CALL AGREGAR_UNIDAD
   RETURN

AGREGAR_CENTENA
LOOP_C1
    MOVLW   B'01100100'  ; CARGA W=100      
    ADDWF NUMEROL, F	 ; SUMA 100 A NUMEROL Y GUARDA EL RESULTADO EN NUMEROL
    BTFSS STATUS, C           
    GOTO CONTINUE_LOOP1       
    INCF NUMEROH, F      ; SI C ES 1, SALTA PARA AUMENTAR EL VALOR DE NUMEROH
CONTINUE_LOOP1
     DECFSZ CENTENA, F	 ; DECREMENTA CENTENA. SI EL RESULTADO ES 0, SALTA A LA SIGUIENTE LINEA
     GOTO LOOP_C1	 ; SI CENTENA NO ES CERO, VUELVE AL INCICIO DEL BUCLE
     RETURN 		 ; CUANDO CENTENA LLEGA A CERO, ESCAPA DE LA SUBRUTINA

AGREGAR_DECENA
LOOP_D1
    MOVLW   B'00001010'  ; CARGA W=10      
    ADDWF NUMEROL, F	 ; SUMA 10 A NUMEROL Y GUARDA EL RESULTADO EN NUMEROL
    BTFSS STATUS, C           
    GOTO CONTINUE_LOOP2       
    INCF NUMEROH, F      ; SI C ES 1, SALTA PARA AUMENTAR EL VALOR DE NUMEROH
CONTINUE_LOOP2
     DECFSZ DECENA, F	 ; DECREMENTA DECENA. SI EL RESULTADO ES 0, SALTA A LA SIGUIENTE LINEA
     GOTO LOOP_D1	 ; SI DECENA NO ES CERO, VUELVE AL INCICIO DEL BUCLE
     RETURN 		 ; CUANDO DECENA LLEGA A CERO, ESCAPA DE LA SUBRUTINA

AGREGAR_UNIDAD
LOOP_U1     
    INCF NUMEROL, F	 ; SUMA 1 A NUMEROL Y GUARDA EL RESULTADO EN NUMEROL
    BTFSS STATUS, C           
    GOTO CONTINUE_LOOP3      
    INCF NUMEROH, F      ; SI C ES 1, SALTA PARA AUMENTAR EL VALOR DE NUMEROH
CONTINUE_LOOP3:
     DECFSZ UNIDAD, F	 ; DECREMENTA UNIDAD. SI EL RESULTADO ES 0, SALTA A LA SIGUIENTE LINEA
     GOTO LOOP_U1	 ; SI UNIDAD NO ES CERO, VUELVE AL INCICIO DEL BUCLE
     RETURN 		 ; CUANDO UNIDAD LLEGA A CERO, ESCAPA DE LA SUBRUTINA 
    
RET_200MS ;RETARDO DE 200MS PARA TESTEOS DE 1 SEGUNDO
   MOVLW .254
   MOVWF REG2,1
LOOP_RET200
   MOVLW .254
   MOVWF REG1,1
DECREMENTAR
   DECFSZ REG1,1
   GOTO DECREMENTAR
   DECFSZ REG2,1
   GOTO LOOP_RET200
   RETURN
TESTER_1SG: ;TESTEO PARA EL CAMBIO, ESPERA 0.4 SEGUNDOS CON ENTER PUESTO Y LUEGO ESPERA EL CLEAR PARA ACTIVAR
   BTFSS C3
   GOTO ENTRA
   CALL RET_200MS
   BTFSS C3
   GOTO ENTRA
   CALL RET_200MS
   BTFSS C3 
   GOTO ENTRA
   BCF L1
   BCF L2 ;SE ELIMINAN LAS DEMAS FILAS PARA QUE NO ESTORBEN
   BCF L3
LOOP_AS ;; ESPERA EL CLEAR, SI SE SUELTA EL ENTER, IGUAL SE SALE
   BTFSS C3
   GOTO ENTRA
   BTFSS C1
   GOTO LOOP_AS
   BTG CAMBIO
   BSF L1
   BSF L2
   BSF L3
   GOTO SALIDA2
   
TESTER_CLEAR_1SG ;; SE ESPERA 1 SEGUNDO PARA BORRAR TODA LA PANTALLA
   BTFSS C1
   GOTO BORRAR
   CALL RET_200MS
   BTFSS C1
   GOTO BORRAR
   CALL RET_200MS
   BTFSS C1 
   GOTO BORRAR
   CALL RET_200MS
   BTFSS C1
   GOTO BORRAR
   CALL RET_200MS
   BTFSS C1
   GOTO BORRAR
   CALL RET_200MS
   CALL RET_200MS
   GOTO INICIO

; ################################## IMPORTANTE: ENTER #######################################
FUNCION_ENTER
   BSF L4
   GOTO TESTER_1SG
ENTRA	; SI NO SE DURA 1 SEGUNDO EN ENTER, VA AQUI
   BSF ENTER	
   MOVLW H'C1'		
   CALL INITS_LCD	; LE LLEVAMOS W COMO INSTRUCCION A LA LCD PARA MOVERNOS A LA SEGUNDA LINEA
   MOVLW .61
   CALL DATO	;LLEVAMOS EL =, COMO DATO 
   MOVF UNIDAD,0  ;YA QUE YA LE DIMOS ENTER, LO GUARDAMOS EN UNIDAD2Y ASI
   MOVWF UNIDAD2
   MOVF DECENA,0
   MOVWF DECENA2
   MOVF CENTENA,0
   MOVWF CENTENA2
   CLRF UNIDAD	; ELIMINAMOS TODO LO GUARDADO EN UNIDAD
   CLRF DECENA
   CLRF CENTENA
   CALL OPERAR
   GOTO SALIDA2
   
TAB_OPE	;PATRON PARA LAS OPERACIONES
   BTFSC SM
   MOVLW H'2B'
   BTFSC RST
   MOVLW H'2D'
   BTFSC ML
   MOVLW H'2A'
   BTFSC DV
   MOVLW H'2F'
   BTFSC CP
   MOVLW H'43'
   BTFSC POT2
   CALL POTSIMB
   BTFSC POT
   MOVLW H'5E'  
   BTFSC FACT
   MOVLW H'21'
   RETURN
POTSIMB  
   MOVLW H'5E'
   CALL DATO
   MOVLW H'32'
   RETURN
GUARDAR
   MOVLW H'30'	
   SUBWF NUMERO,1 ; A NUMERO LE QUITA EL H.30 PARA QUE QUEDE EL NUMERO DECIMAL
   CALL RODAR	; RODAMOS Y GUARDAMOS EN UNIDAD
   MOVF NUMERO,0
   MOVWF UNIDAD
   RETURN
RODAR
   MOVF DECENA,0
   MOVWF CENTENA
   MOVF UNIDAD,0
   MOVWF DECENA
   RETURN
RODAR_REVES
   MOVF DECENA,0
   MOVWF UNIDAD
   MOVF CENTENA,0
   MOVWF DECENA
   CLRF CENTENA
   RETURN
CLEAR
   BSF L4
   GOTO TESTER_CLEAR_1SG ;TESTEA SI ES 1G PARA REINICIAR
BORRAR
   MOVLW .4
   CPFSLT CUENTA
   GOTO ELEGIR_OPE
   CALL RODAR_REVES
   DECF CUENTA,1
   GOTO LCD_C
ELEGIR_OPE
   MOVLW .5
   CPFSEQ CUENTA
   GOTO OTRONUMERO
   CLRF BANDERAS
    MOVF UNIDAD1,0
   MOVWF UNIDAD
   MOVF DECENA1,0
   MOVWF DECENA
   MOVF CENTENA1,0
   MOVWF CENTENA
   MOVLW .3
   MOVWF CUENTA
   GOTO LCD_C
OTRONUMERO
   MOVLW .6
   CPFSEQ CUENTA
   GOTO OTRO_DIGITO
   MOVF UNIDAD1,0
   MOVWF UNIDAD
   MOVF DECENA1,0
   MOVWF DECENA
   MOVF CENTENA1,0
   MOVWF CENTENA
   DECF CUENTA,1
   GOTO LCD_C
OTRO_DIGITO
   CALL RODAR_REVES
   DECF CUENTA,1
LCD_C
   MOVLW H'10' ;SE DEVUELVE UN PASO A LA IZQUIERDA
   CALL INITS_LCD
    MOVLW H'20' 	; AGREGA UN CARCTER NULO " "
    CALL DATO
   MOVLW H'10' ;LO DEJA NUEVAMENTE DONDE SE BORRO
   CALL INITS_LCD
   BCF C1
   GOTO SALIDA    
   
RET_20MS ;RETARDO  DE 20MS PARA LA PANTALLA LCD
   MOVLW .28
   MOVWF REG2,1
LOOP_RET20
   MOVLW .254
   MOVWF REG1,1
DECREMENTAR2
   DECFSZ REG1,1
   GOTO DECREMENTAR2
   DECFSZ REG2,1
   GOTO LOOP_RET20
   RETURN
   END